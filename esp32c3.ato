"""
    ESP32-C3-MINI-1U-N4 minimal circuit, based on Figure 7, page 25
    Datasheet: https://www.espressif.com/sites/default/files/documentation/esp32-c3-mini-1_datasheet_en.pdf
    Released under CERN-OHL-P v2
    Date: 2/15/2024
"""
from "elec/src/ESP32-C3-MINI-1U-N4.ato" import ESP32_C3_MINI_1U_N4
from "generics/resistors.ato" import Resistor
from "generics/capacitors.ato" import Capacitor
from "generics/interfaces.ato" import UART, Power, USB2

module ESP32C3:
    power = new Power
    esp32c3 = new ESP32_C3_MINI_1U_N4

    # strapping pins
    """
        R8 pulls IO8 HIGH to enable startup serial output, pg 12
        R9 pulls IO2 HIGH according to datasheet, pg 12
    """
    r8 = new Resistor
    r8.value = 10kohm +/- 10%
    r8.footprint = "R0603"

    r9 = new Resistor
    r9.value = 10kohm +/- 10%
    r9.footprint = "R0603"

    r8.p1 ~ esp32c3.IO8
    r8.p2 ~ power.vcc
    r9.p1 ~ esp32c3.IO2
    r9.p2 ~ power.vcc

    # ENable
    """
        R1/C3 form an RC delay circuit, EN HIGH = enable chip, EN LOW = chip off, pg 25
    """
    r1 = new Resistor
    r1.value = 10kohm +/- 10%
    r1.footprint = "R0603"
    c3 = new Capacitor
    c3.value = 1uF +/- 40% # TODO: 40% is too large, but is closest match in atopile at the moment
    c3.footprint = "C0603"

    r1.p1 ~ power.vcc
    r1.p2 ~ esp32c3.EN
    esp32c3.EN ~ c3.p1
    c3.p2 ~ power.gnd
    
    # connect power
    power.gnd ~ esp32c3.GND
    power.vcc ~ esp32c3._3V3
    c1 = new Capacitor
    c1.value = 10uF +/- 40% # TODO: 40% is too large, but is closest match in atopile at the moment
    c1.footprint = "C0603"

    c2 = new Capacitor
    c2.value = 0.1uF +/- 40% # TODO: 40% is too large, but is closest match in atopile at the moment
    c2.footprint = "C0603"

    c1.power ~ power
    c2.power ~ power

    # connect USB
    usb = new USB2
    usb.dm ~ esp32c3.IO18
    usb.dp ~ esp32c3.IO19

    # connect UART
    uart = new UART
    uart.tx ~ esp32c3.TXD0
    uart.rx ~ esp32c3.RXD0
    uart.gnd ~ power.gnd